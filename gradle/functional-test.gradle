sourceSets {
    functionalTest {
        groovy.srcDir file('src/functTest/groovy')
        resources.srcDir file('src/functTest/resources')
        compileClasspath = sourceSets.main.output + sourceSets.testSetup.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task createClasspathManifest {
    def outputDir = sourceSets.functionalTest.output.resourcesDir

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    functionalTestCompile files(createClasspathManifest)
}

compileFunctionalTestJava.dependsOn createClasspathManifest

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test, integrationTest
}

check.dependsOn functionalTest

idea.module {
    testSourceDirs += file('src/functTest/groovy')
}