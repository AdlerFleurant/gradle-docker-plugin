buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.ajoberstar:gradle-git:$depVersions.gradleGit"
    }
}

apply plugin: org.ajoberstar.gradle.git.ghpages.GithubPagesPlugin

ext {
    repoPath = 'bmuschko/gradle-docker-plugin'
    ghToken = System.getenv('GH_TOKEN')
}

githubPages {
    if (ghToken) {
        repoUri = "https://github.com/${repoPath}.git"
        credentials {
            username = ghToken
            password = ''
        }
    } else {
        repoUri = "git@github.com:${repoPath}.git"
    }

    pages {
        from(javadoc.outputs.files) {
            into 'docs/javadoc'
        }
        from(groovydoc.outputs.files) {
            into 'docs/groovydoc'
        }
    }
}

publishGhPages.dependsOn javadoc, groovydoc
